#Область ПрограммныйИнтерфейс

// Канонический идентификатор из произвольной строки
//
// Параметры:
//  Значение - Строка - произвольная строка 
// 
// Возвращаемое значение:
//   - Строка
//
Функция КаноническийИдентификатор(Знач Значение) Экспорт
	Стек = Новый Массив;
	ЗаменитьНекорректныеФрагментыИдентификатора(Значение);
	
	Для ПорядокСимвола = 1 По СтрДлина(Значение) Цикл
		
		Символ = Сред(Значение, ПорядокСимвола, 1);
		Если Символ = " " Тогда
			ПорядокСимвола = ПорядокСимвола + 1;
			Символ = ВРег(Сред(Значение, ПорядокСимвола, 1));	
		КонецЕсли;
		
		Стек.Добавить(Символ);
		
	КонецЦикла;
	
	Возврат СтрСоединить(Стек);
	
КонецФункции

// Идентификатор в формате константы из произвольной строки. 
//	Например идентификатора: ACCOUTING_REGISTER_EXT_DIMENSIONS 
//
// Параметры:
//  Значение - Строка - произвольная строка 
// 
// Возвращаемое значение:
//   - Строка
//
Функция ИдентификаторВФорматеКонстанты(Знач Значение) Экспорт
	
	Значение = СтрЗаменить(Значение, " ", "");
	Значение = СтрЗаменить(Значение, "-", "");
	
	Стек = Новый Массив;

	Для ПозицияСимвола = 1 По СтрДлина(Значение) Цикл
		Символ = Сред(Значение, ПозицияСимвола, 1);
		Если Символ = ВРег(Символ) Тогда
			СледующийСимвол = Сред(Значение, ПозицияСимвола + 1, 1);
			Если СледующийСимвол = ВРег(СледующийСимвол) Тогда
				Стек.Добавить(Символ);	
			Иначе
				Если Стек.Количество() = 0 Тогда
					Стек.Добавить(Символ);	
				Иначе
					Стек.Добавить("_");
					Стек.Добавить(Символ);	
				КонецЕсли;
			КонецЕсли;
		Иначе
			Стек.Добавить(ВРег(Символ));	
		КонецЕсли;
	КонецЦикла;
	
	Возврат СтрСоединить(Стек);         
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаменитьНекорректныеФрагментыИдентификатора(Значение)
	
	ПозицияТочки = СтрНайти(Значение, ".");
	Если ПозицияТочки > 0 Тогда
		ЭлементыСтроки = СтрРазделить(Значение, ".");
		Значение = ЭлементыСтроки[0];
	КонецЕсли;
	
	Если СтрНачинаетсяС(Значение, ПрефиксОбъектаМетаданные()) 
		Или СтрНачинаетсяС(Значение, ПрефиксОбъектаМетаданныеАнглийский()) Тогда
		
		Значение = СтрЗаменить(Значение, ПрефиксОбъектаМетаданные(), "");
		Значение = СтрЗаменить(Значение, ПрефиксОбъектаМетаданныеАнглийский(), "");
		Значение = СокрЛП(Значение);
		
	КонецЕсли;
	
	Значение = СтрЗаменить(Значение, "-", " ");	
	
КонецПроцедуры

Функция ПрефиксОбъектаМетаданные()
	Возврат "ОбъектМетаданных:";	
КонецФункции

Функция ПрефиксОбъектаМетаданныеАнглийский()
	Возврат "MetadataObject:";	
КонецФункции

#КонецОбласти