#Область ПрограммныйИнтерфейс

Процедура ПрочитатьКнигу(ПутьДоКниги, ВерсияПлатформы) Экспорт
	
	Чтение = Обработки.ЧтениеСправки.Создать();
	ДеревоСправки = Чтение.ПрочитатьДеревоСправкиИзКниги(ПутьДоКниги);
	
	//ИмяФайла = "D:\DATA\Develop\1c-context\graber\tmp\дерево";
	//ЗначениеВФайл(ИмяФайла, Содержание); 
	//ДеревоСправки = ЗначениеИзФайла(ИмяФайла);
	
	ОбработатьСодержание(ДеревоСправки, ВерсияПлатформы);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ОчиститьБазу() Экспорт
	
	Выборка = Справочники.Свойства.Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.Предопределенный Тогда
			Продолжить;
		КонецЕсли;
		
		Выборка.ПолучитьОбъект().Удалить();
	КонецЦикла;
	
	Выборка = Справочники.ВерсииТипов.Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.Предопределенный Тогда
			Продолжить;
		КонецЕсли;
		
		Выборка.ПолучитьОбъект().Удалить();
	КонецЦикла;
	
	Выборка = Справочники.Типы.Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.Предопределенный Тогда
			Продолжить;
		КонецЕсли;
		
		Выборка.ПолучитьОбъект().Удалить();
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбработатьСодержание(Содержание, ВерсияПлатформы)
	
	Для Каждого СтрокаСодержания Из Содержание.Строки Цикл
		
		ЧастиПути = СтрРазделить(СтрокаСодержания.Путь, "/");
		ИмяФайла = ЧастиПути[ЧастиПути.ВГраница()];
		Если СтрНайти(ИмяФайла, "catalog") > 0 Тогда
			
			// раздел справки 
			ОбработатьСодержание(СтрокаСодержания, ВерсияПлатформы);
			
		Иначе
			
			// это тип
			Имя = Лев(ИмяФайла, СтрДлина(ИмяФайла) - 5);	
			Идентификатор = нРег(СтрЗаменить(Имя, " ", ""));
			Ссылка = Справочники.Типы.НайтиПоРеквизиту("Идентификатор", Идентификатор);
			Если Ссылка.Пустая() Тогда
				Элемент = Справочники.Типы.СоздатьЭлемент();
				Элемент.Наименование = СтрокаСодержания.Имя;
				Элемент.Идентификатор = Идентификатор;
				Элемент.Записать();
				
				Ссылка = Элемент.Ссылка;
			КонецЕсли;
			
			СсылкаНаВерсию = ВерсияТипаПоФильтру(Идентификатор, Ссылка, ВерсияПлатформы);
			Если СсылкаНаВерсию.Пустая() Тогда
				Элемент = Справочники.ВерсииТипов.СоздатьЭлемент();
				Элемент.Идентификатор = Идентификатор;
				Элемент.Владелец = Ссылка;
				Элемент.ВерсияПлатформы = ВерсияПлатформы;
				Элемент.Наименование = Ссылка.Наименование;
				Элемент.Записать();
				СсылкаНаВерсию = Элемент.Ссылка;
			КонецЕсли;
			
			ОбработатьСодержаниеТипа(Ссылка, СсылкаНаВерсию, СтрокаСодержания);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработатьСодержаниеТипа(Тип, ВерсияТипа, СтрокаСодержания)

	Для Каждого ПодчиненныйОбъект Из СтрокаСодержания.Строки Цикл
		
		Если ПустаяСтрока(ПодчиненныйОбъект.Путь) Тогда
			
			Если ПодчиненныйОбъект.Имя = "Свойства" Тогда
				
				Для Каждого СтрокаСвойства Из ПодчиненныйОбъект.Строки Цикл
					
					Если СтрНайти(СтрокаСвойства.Имя, "<") > 0 Тогда
						Продолжить;
					КонецЕсли;
					
					Имя = нРег(СтрокаСвойства.Имя);
					
					СсылкаНаСвойство = СвойствоТипаПоФильтру(Имя, ВерсияТипа, ВерсияТипа.ВерсияПлатформы);
					
					Если СсылкаНаСвойство.Пустая() Тогда
						Элемент = Справочники.Свойства.СоздатьЭлемент();
						Элемент.Идентификатор = Имя;
						Элемент.Наименование = СтрокаСвойства.Имя;
						Элемент.Владелец = ВерсияТипа;
						Элемент.ВерсияПлатформы = ВерсияТипа.ВерсияПлатформы;
						Элемент.Записать();
						
						СсылкаНаСвойство = Элемент.Ссылка;
					КонецЕсли;					
					
				КонецЦикла;
				
			ИначеЕсли ПодчиненныйОбъект.Имя = "Методы" Тогда
				
				Для Каждого СтрокаМетода Из ПодчиненныйОбъект.Строки Цикл
					Если СтрНайти(СтрокаМетода.Имя, "<") > 0 Тогда
						Продолжить;
					КонецЕсли;	
					
					Идентификатор = нРег(СтрокаМетода.Имя);
					
					СсылкаНаМетод = МетодТипаПоФильтру(Идентификатор, ВерсияТипа, ВерсияТипа.ВерсияПлатформы);
					
					Если СсылкаНаМетод.Пустая() Тогда
						Элемент = Справочники.Методы.СоздатьЭлемент();
						Элемент.Наименование = СтрокаМетода.Имя;
						Элемент.Идентификатор = Идентификатор;
						Элемент.Владелец = ВерсияТипа; 
						Элемент.ВерсияПлатформы = ВерсияТипа.ВерсияПлатформы;
						Элемент.Записать();
						
						СсылкаНаМетод = Элемент.Ссылка;
					КонецЕсли;
					
				КонецЦикла;
								
			Иначе
				
				// Конструкторы
				// Параметры формы
				// События
				
			КонецЕсли;		
			
		КонецЕсли;		
		
	КонецЦикла;
	
КонецПроцедуры

Функция МетодТипаПоФильтру(Идентификатор, Владелец, Платформа)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Методы.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.Методы КАК Методы
	               |ГДЕ
	               |	Методы.Идентификатор = &Идентификатор
	               |	И Методы.Владелец = &Владелец
	               |	И Методы.ВерсияПлатформы = &Платформа";
	Запрос.УстановитьПараметр("Идентификатор", Идентификатор);
	Запрос.УстановитьПараметр("Владелец", Владелец);
	Запрос.УстановитьПараметр("Платформа", Платформа);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Справочники.ВерсииТипов.ПустаяСсылка();
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	Возврат Выборка.Ссылка;
	
КонецФункции

Функция СвойствоТипаПоФильтру(Идентификатор, Владелец, Платформа)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Свойства.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.Свойства КАК Свойства
	               |ГДЕ
	               |	Свойства.Идентификатор = &Идентификатор
	               |	И Свойства.Владелец = &Владелец
	               |	И Свойства.ВерсияПлатформы = &Платформа";
	Запрос.УстановитьПараметр("Идентификатор", Идентификатор);
	Запрос.УстановитьПараметр("Владелец", Владелец);
	Запрос.УстановитьПараметр("Платформа", Платформа);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Справочники.ВерсииТипов.ПустаяСсылка();
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	Возврат Выборка.Ссылка;
	
КонецФункции

Функция ВерсияТипаПоФильтру(Идентификатор, Владелец, Платформа)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВерсииТипов.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.ВерсииТипов КАК ВерсииТипов
	               |ГДЕ
	               |	ВерсииТипов.Идентификатор = &Идентификатор
	               |	И ВерсииТипов.Владелец = &Владелец
	               |	И ВерсииТипов.ВерсияПлатформы = &Платформа";
	Запрос.УстановитьПараметр("Идентификатор", Идентификатор);
	Запрос.УстановитьПараметр("Владелец", Владелец);
	Запрос.УстановитьПараметр("Платформа", Платформа);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Справочники.ВерсииТипов.ПустаяСсылка();
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	Возврат Выборка.Ссылка;
	
КонецФункции

#КонецОбласти